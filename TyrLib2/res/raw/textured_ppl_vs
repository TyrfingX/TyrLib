uniform mat4 u_MVPMatrix;       // A constant representing the combined model/view/projection matrix.
uniform mat4 u_MVMatrix;        // A constant representing the combined model/view matrix.
uniform mat4 u_Bone[55]; 		// Array of bones
 
attribute vec4 a_Position;      // Per-vertex position information we will pass in.
attribute vec4 a_Color;         // Per-vertex color information we will pass in.
attribute vec3 a_Normal;        // Per-vertex normal information we will pass in.
attribute vec2 a_TexCoordinate; // Per-vertex texture coordinate information we will pass in.
attribute vec4 a_BoneIndex;		// Per-vertex bone bindings
attribute vec4 a_BoneWeight;	// Per-vertex per bone weighing	

varying vec3 v_Position;        // This will be passed into the fragment shader.
varying vec4 v_Color;           // This will be passed into the fragment shader.
varying vec3 v_Normal;          // This will be passed into the fragment shader.
varying vec2 v_TexCoordinate;   // This will be passed into the fragment shader.
 
// The entry point for our vertex shader.
void main()
{

	vec4 position = vec4(vec3(a_Position), 1.0);
	vec4 normal = vec4(a_Normal, 0.0);
	
	if (int(a_BoneIndex.x) != -1) {
		mat4 boneMatrix =  	a_BoneWeight.x * u_Bone[ int(a_BoneIndex.x) ]  +
			                a_BoneWeight.y * u_Bone[ int(a_BoneIndex.y) ]  +
			                a_BoneWeight.z * u_Bone[ int(a_BoneIndex.z) ]  +
			                a_BoneWeight.w * u_Bone[ int(a_BoneIndex.w) ];
			                
	    position = boneMatrix * position;
	    
		normal = normalize(boneMatrix * normal);
	}



    // Transform the vertex into eye space.
    v_Position = vec3(u_MVMatrix * position);
 
    // Pass through the color.
    v_Color = a_Color;
 
    // Pass through the texture coordinate.
    v_TexCoordinate = a_TexCoordinate;
 
    // Transform the normal's orientation into eye space.
    v_Normal = vec3(u_MVMatrix * normal);
 
    // gl_Position is a special variable used to store the final position.
    // Multiply the vertex by the matrix to get the final point in normalized screen coordinates.
    gl_Position = u_MVPMatrix * position;
}